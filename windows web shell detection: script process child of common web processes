# Identifies suspicious commands executed via a web server, 
# which may suggest a vulnerability and remote shell access.
#
# MITRE ATT&CK - Persistence TA0003 - Initial Access TA0001 - Execution TA0002
#
# Index patterns -winlogbeat, logs-endpoint.events.process-*
#                 logs-windows, endgame-, logs-system.security*, logs-sentinel_one_cloud_funnel*
#
#EQL Query 

process where host.os.type == "windows" and event.type == "start" and
  process.parent.name : ("w3wp.exe", "httpd.exe", "nginx.exe", "php.exe", "php-cgi.exe", "tomcat.exe") and
  process.name : ("cmd.exe", "cscript.exe", "powershell.exe", "pwsh.exe", "powershell_ise.exe", "wmic.exe", "wscript.exe") and
  not
  (
    process.parent.name : ("php.exe", "httpd.exe") and process.name : "cmd.exe" and
    process.command_line : (
      "cmd.exe /c mode CON",
      "cmd.exe /s /c \"mode CON\"",
      "cmd.exe /c \"mode\"",
      "cmd.exe /s /c \"tput colors 2>&1\""
    )
  )

# required field - event.type, host.os.type, process.command_line, process.name, process.parent.name
